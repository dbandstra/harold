deftrack @my_track:
    freq: constant,
    note_on: boolean,
begin
    0.0 (freq=440, note_on=true)
    1.0 (freq=880, note_on=true)
    2.0 (freq=880, note_on=false)
end

defcurve $carrier_curve begin
    0.0  440.0
    0.5  880.0
    1.0  110.0
    1.5  660.0
    2.0  330.0
    3.9   20.0
end

defcurve $modulator_curve begin
    0.0  110.0
    1.5   55.0
    3.0  220.0
end

def CurvePlayerInner:
    carrier_curve: curve,
    modulator_curve: curve,
    freq_mul: constant,
begin
    out SineOsc(
        freq = freq_mul * Curve(curve=carrier_curve, function='linear'),
        phase = SineOsc(
            freq = freq_mul * Curve(curve=modulator_curve, function='smoothstep'),
            phase = 0
        )
    )
end

// replicates example_curve.zig
def CurvePlayer:
    freq_mul: constant,
begin
    out CurvePlayerInner(
        freq_mul,
        carrier_curve = $carrier_curve,
        modulator_curve = $modulator_curve
    )
end

def Square:
    freq: cob,
    note_on: boolean,
begin
    s = max(0, SineOsc(freq, phase=0) * 0.2)
    f = Envelope(
        attack='cubed'(0.01),
        decay='cubed'(0.1),
        release='cubed'(1.0),
        sustain_volume=1,
        note_on
    )
    out s * f * pi
end

def MySineOsc:
    freq: cob,
    phase: cob,
begin
    out sin((Cycle(speed=freq) + phase) * 2 * pi)
end

def InnerInstrument:
    freq: cob,
    note_on: boolean,
begin
    freq = freq * 0.5
    mfreq = freq * (1 + 0.02 * SineOsc(freq=4, phase=0))

    f = Envelope(
        attack='cubed'(0.05),
        decay='cubed'(0.1),
        release='cubed'(1.0),
        sustain_volume=1,
        note_on
    )

    f2 = Envelope(
        attack='cubed'(0.4),
        decay='cubed'(6),
        release='cubed'(6),
        sustain_volume=0.5,
        note_on
    )

    s = f * SineOsc(
        freq=freq * 4 * f2 * SineOsc(freq=mfreq * 0.25, phase=
            SineOsc(freq=mfreq*0.5, phase=0)
        ),
        phase=0)

    out s
    //out Filter(input=s, type='high_pass', cutoff=0.1, res=0)
end

def Echoes:
    input: waveform,
    echo_volume: constant,
    filter_type: FilterType,
begin
    out delay 11025 begin
        result = input + feedback * echo_volume

        out result
        feedback Filter(input=result, type=filter_type, cutoff=0.1, res=0)
    end
end

def Instrument:
    freq: cob,
    note_on: boolean,
begin
    //input = 0.5 * InnerInstrument(freq=freq*2, note_on)
    input = CurvePlayer(freq_mul=1)

    out input
    //out Echoes(input, echo_volume=0.6, filter_type='low_pass')

    //out Square(freq, note_on)
end
